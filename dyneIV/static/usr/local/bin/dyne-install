#!/bin/zsh
#
# Copyright (C) 2023-2024 Dyne.org Foundation
#
# Designed, written and maintained by Denis Roio <jaromil@dyne.org>
#
# This source code is free software; you can redistribute it and/or
# modify it under the terms of the GNU Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This source code is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.	Please refer
# to the GNU Public License for more details.
#
# You should have received a copy of the GNU Public License along with
# this source code; if not, , see <https://www.gnu.org/licenses/>.

source /etc/boot.d/_dyne_utils
mm "dyne-install $*: `date`"

set -e

cmd="${1:-persist}"

dev=`pwd`

[ -w "$dev" ] || {
	mm "needs super-user powers: sudo dyne-install"
	mm "volume not writable: $dev"
	exit 1
}

findmnt -M "${dev}" || {
	mm "needs the root of a storage volume"
	mm "invalid location: $dev"
	exit 1
}

function mkiso() {
	mkdir -p "$dev/dyne"
	xorriso -as mkisofs -iso-level 3 -o "$dev"/dyne/dynebolic.iso \
        -full-iso9660-filenames -volid "DYNEIV" --mbr-force-bootable    \
        -partition_offset 16 -joliet -joliet-long -rational-rock        \
        -isohybrid-mbr /usr/lib/ISOLINUX/isohdpfx.bin -eltorito-boot    \
        isolinux/isolinux.bin -no-emul-boot -boot-load-size 4           \
        -boot-info-table --eltorito-catalog isolinux/isolinux.cat       \
        -eltorito-alt-boot -e --interval:appended_partition_2:all::     \
        -no-emul-boot -isohybrid-gpt-basdat -append_partition 2         \
        `cat /proc/sys/kernel/random/uuid` \
        /var/run/live/medium/efiboot.img /var/run/live/medium         
}

function mkpersist() {
	# default 3.9GiB
	size=${size:-3900}
	[[ -n "$size"	]] || mm "size argument missing, use -s"
	[[ $size == <-> ]] || mm "size must be an integer (MB)"
	[[ $size -ge 512 ]] || mm "size can't be smaller than 512 MB"
	persist="$dev/dyne/dyne.nst"
	mm "creating nest of $size MiB in $persist"
	mkdir -p "$dev/dyne"
	dd if=/dev/zero of="$persist" \
	    bs=1048576 count="$size" oflag=direct status=progress
	loop=`losetup -f`
	losetup -f "$persist"
	mkfs.ext4 -L persistence "$loop"
	mkdir -p /mnt/dynebolic-persistence-temp
	mount "$loop" /mnt/dynebolic-persistence-temp
	cat << EOF > "/mnt/dynebolic-persistence-temp/persistence.conf"
/home union
/root union
/etc union
/var union
/usr union
EOF
	umount /mnt/dynebolic-persistence-temp
	rmdir /mnt/dynebolic-persistence-temp
	losetup -d "$loop"
}

function mkguix() {
	# check if a nested guix already exist
	if [ ! -d "${dev}/dyne/guix-nest" ] ; then
		mm "Guix storage directory not existing, creating it"	
		#mkdir -p ${dev}/dyne/guix-nest
	fi
	# check for content directories
	if [ ! -d "${dev}/dyne/guix-nest/var/guix" ] ; then
		mm "guix directory missing, creating it"
		mkdir -p ${dev}/dyne/guix-nest/var/guix /var/guix
	fi
        if [ ! -d "${dev}/dyne/guix-nest/gnu" ] ; then
	       mm "gnu directory not existing, creating it"
	       mkdir -p ${dev}/dyne/guix-nest/gnu /gnu
	fi
	
	# Binding mount points
	if findmnt /gnu > /dev/null ; then
		mount -o remount,bind ${dev}/dyne/guix-nest/var/guix /var/guix
		mount -o remount,bind ${dev}/dyne/guix-nest/gnu /gnu
	else
		mount -o bind ${dev}/dyne/guix-nest/var/guix /var/guix
		mount -o bind ${dev}/dyne/guix-nest/gnu /gnu
	fi

	# execute guix-setup script for version 1.4.0
	#
	mm "Executing guix-setup script"
	/usr/local/sbin/guix-setup
	date > /var/guix/.dyne
	date > /gnu/.dyne

	# check for profile installation and source it just to be sure
	# if [ -z ${var+x} ]; then echo "var is unset"; else echo "var is set to '$var'"; fi
	if [ ! -x /root/.config/guix/current ] ; then
		mm "Something went wrong with linking guix profile in /root/.config/guix/current"
	else
		mm "Guix profile is linked from \
			${dev}/dyne/guix-nest/var/guix/profiles/per-user/root/current-guix to ~root/.config/guix/current"
		mm "It will be sourced from /etc/profile for new shells"
	fi
}

function mkflatpak() {
		mkdir -p ${dev}/dyne/flatpak
		mount -o bind ${dev}/dyne/flatpak /var/lib/flatpak
		flatpak remote-add --if-not-exists \
				flathub https://flathub.org/repo/flathub.flatpakrepo
		date > /var/lib/flatpak/.dyne
}

case "$cmd" in
	init) ;;
	scan) ;;
	iso|dock)
		[[ -r "${dev}"/dyne/dynebolic.iso ]] && {
			mm "cannot overwrite iso: $dev/dyne/dynebolic.iso"
			exit 1
		}
		mkiso
		mm "--"
		mm "iso succesfully created! reboot to activate."
		mm "--"
		;;
	persist|nest)
		[[ -r "${dev}"/dyne/dyne.nst ]] && {
			mm "cannot overwrite nest: $dev/dyne/dyne.nst"
			exit 1
		}
		mkpersist
		mm "--"
		mm "nest succesfully created! reboot to activate."
		mm "--"
		;;
	flatpak)
		[[ -r /var/lib/flatpak/.dyne ]] && {
			mm "cannot overwrite flatpak:"
			ls -l /var/lib/flatpak
			mm "last access: `cat /var/lib/flatpak/.dyne`"
			exit 1
		}
		mkflatpak
		mm "--"
		mm "flatpak repository succesfully configured!"
		mm "--"
		;;
	guix)
		[[ -r /var/guix/.dyne ]] && {
			mm "cannot overwrite guix:"
			ls -l /var/guix
			mm "last access: `cat /var/guix/.dyne`"
			exit 1
		}
		mkguix
		mm "--"
		mm "guix repository succesfully configured!"
		mm "start a new shell or reboot"
		mm "--"
		;;
	*)
		mm "command not found: $cmd"
		exit 1 ;;
esac


# notes, tests, snippets:

#		[[ -r /run/live/persistence/loop1/persistence.conf ]] && \
#			mm "running system is already nested"
